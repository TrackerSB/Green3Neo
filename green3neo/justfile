set dotenv-load

backend_dir := "./backend"
frb_dart_output_dir := "./lib/backend"
llvmPath := `clang -v 2>&1 | grep 'Selected GCC installation' | rev | cut -d' ' -f1 | rev`
llvmIncludeDir := llvmPath + "/include"

default:
    @just --list

[confirm]
clean:
    git clean -Xfd

start-postgresql:
    sudo systemctl start postgresql

setup-diesel:
    cd {{ backend_dir }} && diesel setup

generate-diesel-schema:
    cd {{ backend_dir }} && diesel print-schema > src/schema.rs

generate-diesel-models:
    cd {{ backend_dir }} && diesel_ext --model --import-types diesel::Queryable --import-types diesel::Selectable --import-types diesel::Identifiable --import-types backend_macros::make_fields_non_final --import-types flutter_rust_bridge::frb --import-types crate::schema::* --derive Queryable,Selectable --add-table-name > src/models.rs
    git apply ./patches/models.rs.patch

# FIXME Verify that FRB versions in Cargo.toml, pubspec.yaml and the installed FRB codegen correspond to each other
generate-flutter-rust-bindings:
    mkdir -p {{ frb_dart_output_dir }}
    flutter_rust_bridge_codegen generate --no-web --no-add-mod-to-lib --llvm-path {{ llvmIncludeDir }} --rust-input "crate::api" --rust-root {{ backend_dir }} --dart-output {{ frb_dart_output_dir }}
    git apply ./patches/models.dart.patch

build-rust:
    cd {{ backend_dir }} && cargo build --release

generate-flutter-reflectable:
    dart run build_runner build --delete-conflicting-outputs

build-flutter:
    flutter build linux

build: setup-diesel generate-diesel-schema generate-diesel-models generate-flutter-rust-bindings build-rust generate-flutter-reflectable build-flutter

run: build
    flutter run

rebuild: clean build
